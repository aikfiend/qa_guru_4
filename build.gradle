plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.8.1'
}

ext.junitVersion = '5.7.0'
ext.allureVersion = '2.13.8'

allure {
    version = allureVersion
    autoconfigure = true
    aspectjweaver = true
    useJUnit5 {
        version = allureVersion
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation([
            'com.codeborne:selenide:5.17.3',
            'org.junit.jupiter:junit-jupiter-api:' + junitVersion,
            'io.qameta.allure:allure-java-commons:' + allureVersion,
            'org.hamcrest:hamcrest-all:1.3'
    ])

    testRuntimeOnly([
            'org.junit.jupiter:junit-jupiter-engine:' + junitVersion,
            'org.slf4j:slf4j-simple:1.7.30'
    ])
}

test {
    useJUnitPlatform()
}

task positiveTests(type: Test) {
    useJUnitPlatform {
        includeTags 'Positive'
    }
}

task negativeTests(type: Test) {
    useJUnitPlatform {
        includeTags 'Negative'
    }
}

task simpleTests(type: Test) {
    useJUnitPlatform {
        filter {
            includeTestsMatching "SimpleTests"
        }
    }
}